module Internal.Field exposing
    ( Field, Attributes, FieldType(..), Status(..)
    , Msg(..), update
    , init, isBlank, map
    , updateAttributes
    , identifier, inputType, max, min, step, name, value
    , label, hint, placeholder, options
    , isGroup, isRepeatable, isRequired, isAutocompleteable
    , errors, setErrors, clearErrors
    , inputIdString, inputStringToValue
    , error, pattern
    , touchTree
    , updateValueWithString
    )

{-|

@docs Field, Attributes, FieldType, Status
@docs Msg, update
@docs init, isBlank, map
@docs updateAttributes
@docs identifier, inputType, max, min, step, name, value
@docs label, hint, placeholder, options
@docs isGroup, isRepeatable, isRequired, isAutocompleteable
@docs errors, setErrors, clearErrors
@docs inputIdString, inputStringToValue
@docs error, pattern
@docs touchTree
@docs updateValueWithString

-}

import Array
import Dict
import Internal.Utils
import Internal.Value exposing (Value)
import List.Extra
import RoseTree.Tree as Tree


type Status
    = Pristine
    | Focused
    | Touched


type FieldType id err
    = Text
    | TextArea
    | Email
    | Password
    | StrictAutocomplete
    | Integer
    | Float
    | Month
    | Date
    | LocalDatetime
    | Select
    | Radio
    | Checkbox
    | Group
    | Repeatable (Field id err)
    | Error (List err)


type alias Attributes id err =
    { inputType : FieldType id err
    , name : Maybe String
    , value : Value
    , isRequired : Bool
    , label : Maybe String
    , placeholder : Maybe String
    , hint : Maybe String
    , min : Value
    , max : Value
    , step : Value
    , autogrow : Bool
    , options : List ( String, Value )
    , identifier : Maybe id
    , status : Status
    , repeatableMin : Int
    , repeatableMax : Maybe Int
    , addFieldsButtonCopy : String
    , removeFieldsButtonCopy : String
    , errors : List err
    , classList : List String
    , selectionStart : Int
    , selectionEnd : Int
    , disabled : Bool
    , visible : Bool
    , pattern : List Internal.Utils.MaskToken
    }


type alias Field id err =
    Tree.Tree (Attributes id err)


init : FieldType id err -> List (Attributes id err -> Attributes id err) -> Attributes id err
init inputType_ =
    List.foldl (<|)
        { inputType = inputType_
        , name = Nothing
        , label = Nothing
        , hint = Nothing
        , placeholder = Nothing
        , value = Internal.Value.blank
        , min = Internal.Value.blank
        , max = Internal.Value.blank
        , step = Internal.Value.blank
        , autogrow = False
        , isRequired = False
        , options = []
        , identifier = Nothing
        , status = Pristine
        , repeatableMin = 1
        , repeatableMax = Nothing
        , addFieldsButtonCopy = "Add"
        , removeFieldsButtonCopy = "Remove"
        , errors = []
        , classList = []
        , selectionStart = 0
        , selectionEnd = 0
        , disabled = False
        , visible = True
        , pattern = []
        }


updateAttributes :
    List (Attributes id err -> Attributes id err)
    -> Field id err
    -> Field id err
updateAttributes attrList =
    Tree.updateValue
        (\attrs ->
            let
                updatedAttrs =
                    List.foldl (<|) attrs attrList
            in
            { updatedAttrs | identifier = attrs.identifier }
        )


{-| A message generated by a user input.
-}
type Msg id
    = InputChanged (List Int) Internal.Value.Value { selectionStart : Int, selectionEnd : Int }
    | InputFocused (List Int)
    | InputBlured (List Int)
    | InputsAdded (List Int)
    | InputsRemoved (List Int)


updateAt :
    List Int
    -> (Field id err -> Field id err)
    -> Field id err
    -> Field id err
updateAt path func input =
    -- WTF?
    case path of
        [] ->
            func input

        _ ->
            Tree.updateAt path func input


update : Msg id -> Field a err -> Field a err
update msg input =
    case msg of
        InputChanged path val selection ->
            updateAt path
                (Tree.updateValue
                    (\attrs ->
                        { attrs
                            | value = val
                            , errors = []
                            , selectionStart = selection.selectionStart
                            , selectionEnd = selection.selectionEnd
                        }
                    )
                )
                input

        InputFocused path ->
            updateAt path (Tree.updateValue focus) input

        InputBlured path ->
            updateAt path (Tree.updateValue blur) input

        InputsAdded path ->
            case
                Tree.getValueAt path input
                    |> Maybe.map .inputType
            of
                Just (Repeatable template) ->
                    updateAt path (Tree.push template) input

                _ ->
                    input

        InputsRemoved path ->
            Tree.removeAt path input


focus : Attributes id err -> Attributes id err
focus input =
    { input
        | status = Focused
        , value =
            if Internal.Value.isInvalid input.value then
                Internal.Value.Blank

            else
                input.value
    }


touchTree : Field id err -> Field id err
touchTree =
    Tree.mapValues (\node -> { node | status = Touched })


blur : Attributes id err -> Attributes id err
blur input =
    { input
        | status = Touched
        , value =
            case input.inputType of
                StrictAutocomplete ->
                    if Internal.Value.isBlank input.value then
                        Internal.Value.Invalid

                    else
                        input.value

                _ ->
                    input.value
    }


isBlank : Field id err -> Bool
isBlank input =
    case Tree.value input |> .inputType of
        Group ->
            False

        Repeatable _ ->
            False

        _ ->
            Internal.Value.isBlank (value input)


identifier : Field id err -> Maybe id
identifier input =
    Tree.value input |> .identifier


value : Field id err -> Value
value input =
    Tree.value input |> .value


name : Field id err -> Maybe String
name input =
    Tree.value input |> .name


placeholder : Field id err -> Maybe String
placeholder input =
    Tree.value input |> .placeholder


label : Field id err -> Maybe String
label input =
    Tree.value input |> .label


hint : Field id err -> Maybe String
hint input =
    Tree.value input |> .hint


inputType : Field id err -> FieldType id err
inputType input =
    Tree.value input |> .inputType


min : Field id err -> Value
min input =
    Tree.value input |> .min


max : Field id err -> Value
max input =
    Tree.value input |> .max


step : Field id err -> Value
step input =
    Tree.value input |> .step


pattern : Field id err -> List Internal.Utils.MaskToken
pattern input =
    Tree.value input |> .pattern


options : Field id err -> List ( String, Value )
options input =
    Tree.value input |> .options


isRequired : Field id err -> Bool
isRequired input =
    Tree.value input |> .isRequired


isAutocompleteable : Field id err -> Bool
isAutocompleteable input =
    case inputType input of
        Text ->
            not (List.isEmpty (options input))

        StrictAutocomplete ->
            True

        _ ->
            False


isGroup : Field id err -> Bool
isGroup input =
    case Tree.value input |> .inputType of
        Group ->
            True

        Repeatable _ ->
            True

        _ ->
            False


isRepeatable : Field id err -> Bool
isRepeatable input =
    case Tree.value input |> .inputType of
        Repeatable _ ->
            True

        _ ->
            False


errors : Field id err -> List err
errors tree =
    case Tree.children tree of
        [] ->
            Tree.value tree |> .errors

        children ->
            (Tree.value tree |> .errors)
                :: List.map errors children
                |> List.concat


setErrors : List err -> Field id err -> Field id err
setErrors errorList =
    Tree.updateValue
        (\input ->
            { input
                | errors = List.Extra.unique (errorList ++ input.errors)
            }
        )


clearErrors : Field id err -> Field id err
clearErrors =
    Tree.updateValue (\input -> { input | errors = [] })


map : (a -> b) -> (err1 -> err2) -> Attributes a err1 -> Attributes b err2
map func errToErr input =
    { inputType = mapFieldType func errToErr input.inputType
    , name = input.name
    , value = input.value
    , isRequired = input.isRequired
    , label = input.label
    , placeholder = input.placeholder
    , hint = input.hint
    , min = input.min
    , max = input.max
    , step = input.step
    , autogrow = input.autogrow
    , options = input.options
    , identifier = Maybe.map func input.identifier
    , status = input.status
    , repeatableMin = input.repeatableMin
    , repeatableMax = input.repeatableMax
    , addFieldsButtonCopy = input.addFieldsButtonCopy
    , removeFieldsButtonCopy = input.removeFieldsButtonCopy
    , errors = List.map errToErr input.errors
    , classList = input.classList
    , selectionStart = input.selectionStart
    , selectionEnd = input.selectionEnd
    , disabled = input.disabled
    , visible = input.visible
    , pattern = input.pattern
    }


mapFieldType : (a -> b) -> (err1 -> err2) -> FieldType a err1 -> FieldType b err2
mapFieldType func errToErr inputType_ =
    case inputType_ of
        Repeatable tree ->
            Repeatable (Tree.mapValues (map func errToErr) tree)

        Error errorList ->
            Error (List.map errToErr errorList)

        Text ->
            Text

        TextArea ->
            TextArea

        Email ->
            Email

        Password ->
            Password

        StrictAutocomplete ->
            StrictAutocomplete

        Integer ->
            Integer

        Float ->
            Float

        Month ->
            Month

        Date ->
            Date

        LocalDatetime ->
            LocalDatetime

        Select ->
            Select

        Radio ->
            Radio

        Checkbox ->
            Checkbox

        Group ->
            Group


inputIdString : Field id err -> String
inputIdString input =
    name input
        |> Maybe.withDefault
            (inputType input |> inputTypeToString)


inputTypeToString : FieldType id err -> String
inputTypeToString type_ =
    case type_ of
        Text ->
            "text"

        StrictAutocomplete ->
            "text"

        TextArea ->
            "textarea"

        Email ->
            "email"

        Password ->
            "password"

        Integer ->
            "integer"

        Float ->
            "float"

        Month ->
            "month"

        Date ->
            "date"

        LocalDatetime ->
            "datetime-local"

        Select ->
            "select"

        Radio ->
            "radio"

        Checkbox ->
            "checkbox"

        Repeatable _ ->
            "repeatable"

        Group ->
            "group"

        Error _ ->
            "error"


updateValueWithString : String -> Field id err -> Field id err
updateValueWithString str field =
    Tree.updateValue
        (\attrs -> { attrs | value = inputStringToValue field str })
        field



-- Debug.todo "crash"


inputStringToValue : Field id err -> String -> Value
inputStringToValue input str =
    let
        unwrappedField =
            Tree.value input

        getChoice () =
            case String.toInt str of
                Just idx ->
                    Array.fromList unwrappedField.options
                        |> Array.get idx
                        |> Maybe.map Tuple.second
                        |> Maybe.withDefault Internal.Value.blank

                Nothing ->
                    Internal.Value.blank
    in
    case unwrappedField.inputType of
        Text ->
            Internal.Value.fromNonBlankString str

        TextArea ->
            Internal.Value.fromNonEmptyString str

        Password ->
            Internal.Value.fromNonBlankString str

        StrictAutocomplete ->
            Dict.fromList unwrappedField.options
                |> Dict.get str
                |> Maybe.withDefault Internal.Value.blank

        Email ->
            Internal.Value.fromNonBlankString str

        Integer ->
            Internal.Value.intFromString str

        Float ->
            Internal.Value.floatFromString str

        Month ->
            Internal.Value.monthFromString str

        Date ->
            Internal.Value.dateFromString str

        LocalDatetime ->
            Internal.Value.timeFromString str

        Select ->
            getChoice ()

        Radio ->
            getChoice ()

        Checkbox ->
            case str of
                "true" ->
                    Internal.Value.fromBool True

                "false" ->
                    Internal.Value.fromBool False

                _ ->
                    Internal.Value.blank

        Group ->
            Internal.Value.blank

        Repeatable _ ->
            Internal.Value.blank

        Error _ ->
            Internal.Value.blank


error : List err -> Field id err
error errorList =
    Tree.leaf (init (Error errorList) [])
