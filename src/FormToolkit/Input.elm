module FormToolkit.Input exposing
    ( Input, Msg, update, toHtml, toView
    , text, textarea, email, password
    , int, float
    , date, month
    , select, radio, checkbox
    , group, repeatable
    , Attribute
    , name, identifier, value, required, label, placeholder, hint
    , options, min, max
    , noattr
    , inline, copies, repeatableMin, repeatableMax
    , errors
    , map
    )

{-| Provides types and functions to create and manage form inputs, including
various input types, attributes, and error handling.


# Input

@docs Input, Msg, update, toHtml, toView


# Input types

@docs text, textarea, email, password
@docs int, float
@docs date, month
@docs select, radio, checkbox
@docs group, repeatable


# Attributes

@docs Attribute
@docs name, identifier, value, required, label, placeholder, hint
@docs options, min, max
@docs noattr


### Group

@docs inline, copies, repeatableMin, repeatableMax


# Errors

@docs errors


# Advanced

@docs map

-}

import FormToolkit.Decode exposing (Decoder, Error(..))
import FormToolkit.Value as Value
import FormToolkit.View as View
import Html exposing (Html)
import Internal.Input as Internal exposing (Input(..), Msg(..))
import Internal.Value
import RoseTree.Tree as Tree


type alias Tree id val =
    Internal.Tree id val (Error id val)


{-| Represents a form input element, which can be a field or a group of fields.

Can be individual input fields (like text, email, password) or composite inputs
like groups and repeatable groups.

-}
type alias Input id val =
    Internal.Input id val (Error id val)


{-| A message generated by a user input.
-}
type alias Msg id =
    Internal.Msg id


{-| Update a form passing a decoder to validate and produce a result,
and a [Msg](#Msg) to reflect user interactions.
-}
update :
    Decoder id val a
    -> Msg id
    -> Input id val
    -> ( Input id val, Result (List (Error id val)) a )
update decoder msg input =
    FormToolkit.Decode.validateAndDecode decoder <|
        case msg of
            InputChanged path str ->
                updateAt path (updateInput str) input

            InputChecked path bool ->
                updateAt path (updateInputWithBool bool) input

            InputFocused path ->
                updateAt path (Tree.updateValue Internal.focus) input

            InputBlured path ->
                updateAt path (Tree.updateValue Internal.blur) input

            InputsAdded path ->
                case
                    Tree.getValueAt path (toTree input)
                        |> Maybe.map .inputType
                of
                    Just (Internal.Repeatable template) ->
                        updateAt path (Tree.push template) input

                    _ ->
                        input

            InputsRemoved path ->
                Input (Tree.removeAt path (toTree input))


{-|

    view : Html (Never -> a)
    view =
        Input.group []
            [ Input.text [ Input.label "First Name" ]
            , Input.text [ Input.label "Last Name" ]
            ]
            |> Input.toHtml (always never)

-}
toHtml : (Msg id -> msg) -> Input id val -> Html msg
toHtml tagger input =
    View.fromInput tagger input |> View.toHtml


{-| Convert the input to [View](FormToolkit.View#View) as a step before
rendering it to HTML in order to have it customized.

    view : View id (Input.Msg id)
    view =
        Input.group []
            [ Input.text [ Input.label "First Name" ]
            , Input.text [ Input.label "Last Name" ]
            ]
            |> Input.toView (always never)

-}
toView : (Msg id -> msg) -> Input id val -> View.View id val msg
toView =
    View.fromInput


updateInput : String -> Tree id val -> Tree id val
updateInput string =
    Tree.updateValue (Internal.updateValueWithString string)


updateInputWithBool : Bool -> Tree id val -> Tree id val
updateInputWithBool bool =
    Tree.updateValue (Internal.updateValue (Internal.Value.fromBool bool))


updateAt : List Int -> (Tree id val -> Tree id val) -> Input id val -> Input id val
updateAt path func input =
    Input (Tree.updateAt path func (toTree input))


{-| Creates a text input field.

    text [ label "Username", placeholder "Enter your username" ]

-}
text : List (Attribute id val) -> Input id val
text =
    init Internal.Text


{-| Creates a textarea input field.

    textarea [ label "Comments", placeholder "Enter your comments here" ]

-}
textarea : List (Attribute id val) -> Input id val
textarea =
    init Internal.TextArea


{-| Creates an email input field.

    email [ label "Email", required True ]

-}
email : List (Attribute id val) -> Input id val
email =
    init Internal.Email


{-| Creates a password input field.

    password [ label "Password", required True ]

-}
password : List (Attribute id val) -> Input id val
password =
    init Internal.Password


{-| Creates an integer input field.

    int [ label "Age", min (Value.int 0), max (Value.int 120) ]

-}
int : List (Attribute id val) -> Input id val
int =
    init Internal.Integer


{-| Creates a floating-point number input field.

    float [ label "Price", min (Value.float 0.0) ]

-}
float : List (Attribute id val) -> Input id val
float =
    init Internal.Float


{-| Creates a date input field.

    date [ label "Birthdate", required True ]

-}
date : List (Attribute id val) -> Input id val
date =
    init Internal.Date


{-| Creates a month input field.

    month [ label "Expiry Month" ]

-}
month : List (Attribute id val) -> Input id val
month =
    init Internal.Month


{-| Creates a select input field (dropdown).

    select
        [ label "Language"
        , options
            [ ( "EspaÃ±ol", Value.string "ES" )
            , ( "English", Value.string "EN" )
            ]
        ]

-}
select : List (Attribute id val) -> Input id val
select =
    init Internal.Select


{-| Creates a radio button input field.

    radio
        [ label "Light is"
        , options
            [ ( "On", Value.bool True )
            , ( "Off", Value.bool False )
            ]
        ]

-}
radio : List (Attribute id val) -> Input id val
radio =
    init Internal.Radio


{-| Creates a checkbox input field.

    checkbox [ label "Subscribe to newsletter" ]

-}
checkbox : List (Attribute id val) -> Input id val
checkbox =
    init Internal.Checkbox


{-| Creates a group of inputs.

Groups multiple inputs together.

    group []
        [ text [ name "firstName", label "First Name" ]
        , text [ name "lastName", label "Last Name" ]
        ]

-}
group : List (Attribute id val) -> List (Input id val) -> Input id val
group attributes inputs =
    List.map toTree inputs
        |> Tree.branch
            (Internal.init Internal.Group (unwrapAttrs attributes))
        |> Input


{-| Creates a repeatable group of inputs.

Allows inputs to be repeated multiple times.

    repeatable [ name "emails", repeatableMin 1, repeatableMax 5 ]
        (text [ placeholder "Enter email address" ])
        []

-}
repeatable : List (Attribute id val) -> Input id val -> List (Input id val) -> Input id val
repeatable attributes template inputs =
    let
        params =
            Internal.init (Internal.Repeatable (toTree template))
                (unwrapAttrs attributes)

        children =
            List.map toTree inputs
                ++ List.repeat (params.repeatableMin - List.length inputs)
                    (toTree template)
    in
    Input (Tree.branch params children)


init : Internal.InputType id val (Error id val) -> List (Attribute id val) -> Input id val
init inputType attributes =
    Input (Tree.leaf (Internal.init inputType (unwrapAttrs attributes)))


unwrapAttrs :
    List (Attribute id val)
    -> List (Internal.Attrs id val (Error id val) -> Internal.Attrs id val (Error id val))
unwrapAttrs =
    List.map (\(Attribute f) -> f)


toTree : Input id val -> Tree id val
toTree (Input tree) =
    tree


{-| Represents an attribute that can be applied to an input.
-}
type Attribute id val
    = Attribute (Internal.Attrs id val (Error id val) -> Internal.Attrs id val (Error id val))


{-| Sets the name of an input.
-}
name : String -> Attribute id val
name str =
    Attribute (\input -> { input | name = Just str })


{-| Sets the identifier of an input.
-}
identifier : id -> Attribute id val
identifier id =
    Attribute (\input -> { input | identifier = Just id })


{-| Sets the value of an input.
-}
value : Value.Value val -> Attribute id val
value (Value.Value inputValue) =
    Attribute (\input -> { input | value = inputValue })


{-| Marks an input as required.
-}
required : Bool -> Attribute id val
required bool =
    Attribute (\input -> { input | isRequired = bool })


{-| Sets the label of an input.
-}
label : String -> Attribute id val
label str =
    Attribute (\input -> { input | label = Just str })


{-| Sets the placeholder text of an input.
-}
placeholder : String -> Attribute id val
placeholder str =
    Attribute (\input -> { input | placeholder = Just str })


{-| Sets a hint or help text for an input.
-}
hint : String -> Attribute id val
hint str =
    Attribute (\input -> { input | hint = Just str })


{-| Sets the options for a select, radio, or checkbox input.
-}
options : List ( String, Value.Value val ) -> Attribute id val
options values =
    Attribute
        (\input ->
            { input
                | options =
                    List.map (Tuple.mapSecond (\(Value.Value val) -> val))
                        values
            }
        )


{-| Sets the minimum value for an input when the value is scalar (int, float,
date, ...).
-}
min : Value.Value val -> Attribute id val
min (Value.Value val) =
    Attribute (\input -> { input | min = val })


{-| Sets the maximum value for an input when the value is scalar (int, float,
date, ...).
-}
max : Value.Value val -> Attribute id val
max (Value.Value val) =
    Attribute (\input -> { input | max = val })


{-| An attribute that does nothing.
-}
noattr : Attribute id val
noattr =
    Attribute identity


{-| Sets whether the inputs in a group are displayed inline.
-}
inline : Bool -> Attribute id val
inline bool =
    Attribute (\input -> { input | inline = bool })


{-| Sets the text for the add and remove buttons in a repeatable input.
-}
copies : { addButton : String, removeButton : String } -> Attribute id val
copies { addButton, removeButton } =
    Attribute
        (\input ->
            { input
                | addInputsText = addButton
                , removeInputsText = removeButton
            }
        )


{-| Sets the minimum number of copies for a repeatable input.
-}
repeatableMin : Int -> Attribute id val
repeatableMin integer =
    Attribute (\input -> { input | repeatableMin = integer })


{-| Sets the maximum number of copies for a repeatable input.
-}
repeatableMax : Int -> Attribute id val
repeatableMax integer =
    Attribute (\input -> { input | repeatableMax = Just integer })


{-| Collects all errors from an input and its children.
-}
errors : Input id val -> List (Error id val)
errors (Input tree) =
    case Tree.children tree of
        [] ->
            Tree.value tree |> .errors

        children ->
            (Tree.value tree |> .errors)
                :: List.map (errors << Input) children
                |> List.concat


{-| Transforms identifiers or errors in an input, useful to combine inputs with
identifier of different type.

    type PersonFields
        = PersonName
        | PersonAge

    type TeamFields
        = TeamName
        | TeamMembers
        | MemberFields PersonFields

    personsFields : Input PersonFields
    personsFields =
        Input.group []
            [ Input.text
                [ Input.label "Member Name"
                , Input.identifier PersonName
                ]
            , Input.int
                [ Input.label "Member Age"
                , Input.identifier PersonAge
                ]
            ]

    teamFields : Input.Input TeamFields
    teamFields =
        Input.group []
            [ Input.text
                [ Input.label "Team Name"
                , Input.identifier TeamName
                ]
            , Input.repeatable
                [ Input.identifier TeamMembers ]
                (Input.map MemberFields personFields)
                []
            ]

-}
map : (a -> b) -> Input a val -> Input b val
map func (Input tree) =
    Input (Tree.mapValues (Internal.map func (mapError func)) tree)


mapError : (a -> b) -> Error a val -> Error b val
mapError func error =
    case error of
        ValueTooLarge id params ->
            ValueTooLarge (Maybe.map func id) params

        ValueTooSmall id params ->
            ValueTooSmall (Maybe.map func id) params

        ValueNotInRange id params ->
            ValueNotInRange (Maybe.map func id) params

        IsBlank id ->
            IsBlank (Maybe.map func id)

        ParseError id ->
            ParseError (Maybe.map func id)

        ListError id params ->
            ListError (Maybe.map func id)
                { index = params.index
                , error = mapError func params.error
                }

        RepeatableHasNoName id ->
            RepeatableHasNoName (Maybe.map func id)

        InputNotFound id ->
            InputNotFound (func id)

        CustomError err ->
            CustomError err
